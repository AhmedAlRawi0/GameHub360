namespace ca.mcgill.ecse321.GameShop.model;

class GameShop
{
  Integer gameShopId;
  1 <@>- * CustomerAccount customerAccounts;
  1 <@>- * EmployeeAccount employeeAccounts;
  1 <@>- 1 ManagerAccount managerAccount; 
  1 <@>- * GameCategory gameCategories;
  1 <@>- * Game games;
  1 <@>- * GameRequest gameRequests;
  1 <@>- * Order orders;
  1 <@>- 1 StoreInformation storeInformation;
  1 <@>- * Review reviews; 
}

class Account {
  abstract;
  immutable String email; 
  String password;
  lazy String name;
  lazy String phoneNumber;
}

class CustomerAccount {
  isA Account;
  Integer customerId;
  1 cardOwner <@>- * PaymentDetails paymentCards;
}

class StaffAccount {
  abstract;
  isA Account;
}

class EmployeeAccount {
  isA StaffAccount;
  Integer employeeId;
  1 employee <@>- * ActivityLog logs;
  Boolean isActive;
}

class ManagerAccount {
  isA StaffAccount;
  Integer managerId;
}

class ActivityLog {
  Integer logId;
  String content;
}

class RequestNote {
  Integer noteId;
  String content;
  Date noteDate;
}

class PaymentDetails {
  Integer paymentDetailsId;
  String cardName;
  String postalCode;
  Integer cardNumber;
  Integer expMonth;
  Integer expYear;
}

class Order {
  Integer orderId;
  enum OrderStatus { SHIPPING, DELIVERED, RETURNED }
  lazy OrderStatus orderStatus;
  Date orderDate;
}

class Game {
  isA GameEntity; 
  Integer gameId;
  Integer quantityInStock;
  Boolean isAvailable;
  Integer price; 
}

class GameRequest {
  isA GameEntity; 
  Integer requestId;
  1 gameRequest <@>- * RequestNote associatedNotes;
  
  enum RequestStatus { SUBMITTED, APPROVED, REFUSED }
  lazy RequestStatus requestStatus;
  Date requestDate;
}

class GameEntity {
  abstract;
  String name;
  String description;
  String imageURL;
}

class GameCategory {
  Integer categoryId;
  enum CategoryType { GENRE, CONSOLE }
  lazy CategoryType categoryType;
  Boolean isAvailable;
  String name;
}

class Promotion {
  Integer promotionId;
  enum PromotionType { GAME, CATEGORY }
  lazy PromotionType promotionType;
  Double discountPercentageValue;
}

class StoreInformation {
  Integer storeInfoId;
  1 info <@>- * Promotion currentPromotions; 
  lazy String storePolicy; 
}

class Review {
  Integer reviewId;
  1 reviewRecord <@>- * Reply reviewReplies; 

  enum GameReviewRating { ONE_STAR, TWO_STARS, THREE_STARS, FOUR_STARS, FIVE_STARS }
  lazy GameReviewRating rating;
  lazy String comment;
  Date reviewDate;
}

class Reply {
  Integer replyId;
  String content;
  Date replyDate;
}

association {
  1 CustomerAccount reviewAuthor -- * Review reviews;
  1 Order reviewedOrder -- 1 Review orderReview;
  1 CustomerAccount orderedBy -- * Order orderHistory;
  * CustomerAccount wishlist -- * Game wishListedGames;
  1 PaymentDetails paymentInformation -- * Order paidOrders;
  * Order order -- 1..* Game games;
  * Promotion promotions -- * Game promotedGames;
  * Promotion promotions -- * GameCategory promotedCategories;
  * GameEntity games -- 1..* GameCategory categories;
  1 EmployeeAccount requestPlacer -- * GameRequest requests; 
  1 ManagerAccount reviewer -- * Reply reviewReplies; 
  0..1 StaffAccount notesWriter -- 1..* RequestNote writtenNotes; 
}
